apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.ecorengia.org.cinemaapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix '.debug'
        }
    }
    flavorDimensions "environment"
    productFlavors {
        live {
            dimension "environment"

            // TODO: Add a valid TMDB API key here in order to enable this app
            buildConfigField "String", "TMDB_BASE_URL", '"https://api.themoviedb.org/3/"'
            buildConfigField "String", "TMDB_THUMBS_URL", '"https://image.tmdb.org/t/p/w500/"'
            buildConfigField "String", "TMDB_FULL_THUMBS_URL", '"https://image.tmdb.org/t/p/w1280/"'
            buildConfigField "String", "TMDB_API_KEY", '"00000000000000000000000000000000"'
        }
        local {
            dimension "environment"

            buildConfigField "String", "TMDB_BASE_URL", '"https://localhost/3/"'
            buildConfigField "String", "TMDB_THUMBS_URL", '"https://localhost/t/p/w500/"'
            buildConfigField "String", "TMDB_FULL_THUMBS_URL", '"https://localhost/t/p/w1280/"'
            buildConfigField "String", "TMDB_API_KEY", '"00000000000000000000000000000000"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        quiet true
        abortOnError true
        checkAllWarnings false
        warningsAsErrors true
        showAll true
        lintConfig file("${project.rootDir}/config/lint/lint.xml")
        textReport true
        textOutput 'stdout'
        xmlReport true
        xmlOutput file("${project.buildDir}/reports/lint/lint-report.xml")
        htmlReport true
        htmlOutput file("${project.buildDir}/reports/lint/lint-report.html")
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks.
        checkReleaseBuilds false
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'README.md'
        exclude 'META-INF/rxjava.properties'
    }
}

ext {
    support_lib_version = "27.1.1"
    constraint_layout_version = "1.1.2"
    play_services_auth_version = "15.0.1"
    lifecycle_version = "1.1.1"
    retrofit_version = "2.3.0"
    okhttp3_version = "3.9.1"
    room_version = "1.1.1"
    rx_java_version = "2.1.8"
    rx_android_version = "2.0.1"
    dagger_version = "2.16"
    butterknife_version = "8.8.1"
    timber_version = "4.6.0"
    glide_version = "4.7.1"
    auto_version = "1.5.3"
    auto_parcel_ext_version = "0.2.6"
    auto_moshi_ext_version = "0.4.5"
    joda_time_version = "2.9.9"
    espresso_version = "3.0.2"
    junit_version = "4.12"
    test_runner_version = "1.0.2"
    mockito_version = "2.13.0"
    robolectric_version = "3.7.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "com.android.support:cardview-v7:$support_lib_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // Play services
    implementation "com.google.android.gms:play-services-auth:$play_services_auth_version"

    // Lifecycle components
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:support-v4:27.1.1'
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycle_version"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

    // Persistence
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"

    // Reactive extensions
    implementation "io.reactivex.rxjava2:rxjava:$rx_java_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"

    // Dependency and View injection
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"

    // Auto code generators
    compileOnly "com.google.auto.value:auto-value:$auto_version"
    annotationProcessor "com.google.auto.value:auto-value:$auto_version"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:$auto_parcel_ext_version"

    // Image handling
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    // Time
    implementation 'joda-time:joda-time:2.9.9'

    // Testing
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    androidTestImplementation "com.android.support.test:runner:$test_runner_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$okhttp3_version"
    androidTestImplementation "android.arch.persistence.room:testing:$room_version"
    androidTestImplementation "com.google.dagger:dagger:$dagger_version"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
}
